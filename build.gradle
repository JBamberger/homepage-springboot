buildscript {
    ext {
        kotlinVersion = '1.3.11'
        springBootVersion = '2.1.1.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}")
    }
}

plugins {
    id 'org.hidetake.ssh' version '2.9.0'
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'kotlin-jpa'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'


remotes {
    webServer {
        host = '5.1.89.111'
        user = 'deployment'
        port = 2710
        identity = file("${System.properties['user.home']}/.ssh/id_rsa")
        passphrase = server_5_1_89_111_ssh_passphrase
        sudoPassword = server_5_1_89_111_user_deployment_passphrase
        errorStream = System.err
        outputStream = System.out
    }
}

group = 'de.jbamberger.java'
version = rootProject.file('VERSION').text.trim()


def remote_dir = '/var/www/homepage'
def local_dir = 'build/libs'
def deploy_file = "homepage-${version}.jar"


def java_version = JavaVersion.VERSION_1_8

sourceCompatibility = java_version
targetCompatibility = java_version

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = java_version
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = java_version
    }
}

repositories {
    mavenCentral()
}


dependencies {
    implementation('org.springframework.boot:spring-boot-starter-actuator')
//    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springframework.boot:spring-boot-starter-security')
    implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('com.fasterxml.jackson.module:jackson-module-kotlin')
//    implementation('org.flywaydb:flyway-core')
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    implementation("org.jetbrains.kotlin:kotlin-reflect")

    runtimeOnly('org.springframework.boot:spring-boot-devtools')
//    runtimeOnly('com.h2database:h2')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.springframework.security:spring-security-test')
    testImplementation("junit:junit")
}

task('deploy') {
    dependsOn build, test, check
    doLast {
        ssh.run {
            session(remotes.webServer) {
                put from: "${projectDir}/${local_dir}/${deploy_file}", into: remote_dir
                execute "ln -sf ${remote_dir}/${deploy_file} ${remote_dir}/homepage.jar"
                executeSudo('systemctl restart homepage', pty: true)
            }
        }
    }
}